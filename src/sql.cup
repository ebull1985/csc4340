/** Import the class java_cup.runtime.*  */
import java_cup.runtime.*;
import java.util.ArrayList;

/* Parser code to change the way the parser reports errors (include
   line and column number of the error). */
parser code {:
    
    /* Change the method report_error so it will display the line and
       column of where the error occurred in the input as well as the
       reason for the error which is passed into the method in the
       String 'message'. */
    public void report_error(String message, Object info) {
    	/* Create a StringBuffer called 'm' with the string 'Error' in it. */
        StringBuffer m = new StringBuffer("Error");
   
        /* Check if the information passed to the method is the same
           type as the type java_cup.runtime.Symbol. */
        if (info instanceof java_cup.runtime.Symbol) {
            /* Declare a java_cup.runtime.Symbol object 's' with the
               information in the object info that is being typecasted
               as a java_cup.runtime.Symbol object. */
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
   
            /* Check if the line number in the input is greater or
               equal to zero. */
            if (s.left >= 0) {                
                /* Add to the end of the StringBuffer error message
                   the line number of the error in the input. */
                m.append(" in line "+(s.left+1));   
                /* Check if the column number in the input is greater
                   or equal to zero. */
                if (s.right >= 0)                    
                    /* Add to the end of the StringBuffer error message
                       the column number of the error in the input. */
                    m.append(", column "+(s.right+1));
            }
        }
   
        /* Add to the end of the StringBuffer error message created in
           this method the message that was passed into this method. */
        m.append(" : "+message);
   
        /* Print the contents of the StringBuffer 'm', which contains
           an error message, out on a line. */
        throw new Error("Syntax Error " + m.toString());
    }
   
    /* Change the method report_fatal_error so when it reports a fatal
       error it will display the line and column number of where the
       fatal error occurred in the input as well as the reason for the
       fatal error which is passed into the method in the object
       'message' and then exit.*/
    public void report_fatal_error(String message, Object info) {
    	try {
    		report_error(message, info);
    	} catch (Error e) {
        	throw new Error(e.getMessage());
        }
    }
:};


/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
/* Terminals (tokens returned by the scanner).  

   Terminals that have no value are listed first and then terminals
   that do have an value, in this case an integer value, are listed on
   the next line down. */
terminal            SEMI, 
                    SELECT,
                    DISTINCT, 
                    FROM, 
                    WHERE, 
                    AND, 
                    NOT, 
                    COMPARISON, 
                    IN, 
                    EXISTS, 
                    COMMA, 
                    DOT, 
                    LPAREN, 
                    RPAREN;

terminal String     STRING, NAME;

terminal Integer    INTNUM;

/* Non terminals used in the grammar section.  

   Non terminals that have an object value are listed first and then
   non terminals that have an integer value are listed.  An object
   value means that it can be any type, it isn't set to a specific
   type.  So it could be an Integer or a String or whatever. */
                   
non terminal SQLNode                sql_start,
                                    select_stmt;
                                    
non terminal ArrayList<Column>      select_list;

non terminal Boolean                opt_distinct;

non terminal Column                 column_ref;
                                    
non terminal ArrayList<String[]>    from_clause,
                                    table_ref_commalist;
                                    
non terminal String[]               table_ref;

non terminal String                 table, tableAlias;

non terminal ArrayList<WhereNode>   opt_where_clause,
                                    where_clause,
                                    search_condition;
                                    
non terminal WhereNode              predicate,
                                    pred,
                                    comparison_pred,
                                    in_pred,
                                    exists_pred;
                                    
non terminal Scalar                 literal,
                                    scalar_exp;

/*------------------------------ Grammar ------------------------------*/
/*
    <sql_start> ::=<select_stmt> SEMI
    <select_stmt> ::= SELECT <opt_distinct> <select_list> <from_clause> <opt_where_clause>
    <opt_distinct> ::= / empty / | DISTINCT
    <select_list> ::= <column_ref> | <select_list> ',' <column_ref>
    <column_ref> ::= NAME | NAME '.' NAME
    <from_clause> ::= FROM <table_ref_commalist>
    <table_ref_commalist> ::= <table_ref> | <table_ref_commalist> ',' <table_ref>
    <table_ref> ::= <table> | <table> <range_variable>
    <table> ::= NAME
    <range_variable> ::= NAME
    <opt_where_clause> ::= / empty / | <where_clause>
    <where_clause> ::= WHERE <search_condition>
    <search_condition> ::= <predicate> | <search_condition> AND <predicate>
    <predicate> ::= <pred> | NOT <pred>
    <pred> ::= <comparison_pred> | <in_pred> | <exists_pred>
    <comparison_pred> ::= <scalar_exp> COMPARISON <scalar_exp>
    <scalar_exp> ::= <literal> | <column_ref>
    <literal> ::= STRING | INTNUM
    <in_pred> ::= <scalar_exp> IN '(' <select_stmt> ')'
    <exists_pred> ::= EXISTS '(' <select_stmt> ')'
------------------------------------------------------------------------*/

/* Returns SQLNode */
sql_start             ::= select_stmt:s SEMI
            {:  System.out.println("\nNo Syntax Error\n");
            SQLNode child = s;
            SQLNode sqlNode = new SQLNode();
            child.setParent(sqlNode);
            RESULT = sqlNode; :};

/* Returns SQLNode */
select_stmt 	        ::= SELECT opt_distinct:o select_list:s from_clause:r opt_where_clause:owc
            {: SQLNode sqlNode = new SQLNode(o, s, r);
            for(Column col : s) col.setSN(sqlNode);
            if(owc != null) sqlNode.setWhereNodes(owc);
            System.out.println(sqlNode.toString());
            RESULT = sqlNode; :};
            
/* Return boolean true or false */
opt_distinct 	        ::= /*empty*/
            {: RESULT = false; :}
                            |
                            DISTINCT
            {: RESULT = true; :};

/* Return ArrayList<Column>*/ 
select_list 	        ::= column_ref:c
            {: ArrayList<Column> cols = new ArrayList<Column>();
            cols.add(c);
            RESULT = cols; :}
                            | select_list:s COMMA column_ref:c
            {: ArrayList<Column> cols = s;
            cols.add(c);
            RESULT = cols; :};

/* Return Column */
column_ref 	        ::= NAME:n
            {: RESULT = new Column(n); :}
                        | NAME:p DOT NAME:n
            {:  RESULT = new Column(p, n); :};
            
/* Return ArrayList<String[]> */
from_clause 	        ::= FROM table_ref_commalist:ts
            {:  RESULT = ts; :};
            
/* Return ArrayList<String[]> */
table_ref_commalist 	::= table_ref:t
            {: ArrayList<String[]> tables = new ArrayList<String[]>();
            tables.add(t);
            RESULT = tables; :}
                            | table_ref_commalist:ts COMMA table_ref:t
            {: ArrayList<String[]> tables = ts;
            tables.add(t);
            RESULT = tables; :};
            
/* Return String[]. [0]: table name, [1]: table alias */
table_ref 	        ::= table:t
            {: String[] tref = new String[2];
            tref[0] = new String(t);
            tref[1] = null;
            RESULT = tref; :}
                        | table:t tableAlias:a
            {:  String[] tref = new String[2];
            tref[0] = new String(t);
            tref[1] = new String(a);
            RESULT = tref; :};
            
/* Return String */
table 	            ::= NAME:n
            {:  RESULT = n; :};
            
/* Return String */
tableAlias 	    ::= NAME:n
            {:  RESULT = n; :};

/* Return null or ArrayList<WhereNode> */
opt_where_clause 	    ::= /*empty*/
            {: RESULT = null; :}
                            | where_clause:wns
            {:  RESULT = wns; :};
            
/* Return ArrayList<WhereNode> */
where_clause 	        ::= WHERE search_condition:wns
            {:  RESULT = wns; :};
            
/* Return ArrayList<WhereNode> */
search_condition 	    ::= predicate:p
            {: ArrayList<WhereNode> whereNodes = new ArrayList<WhereNode>();
            whereNodes.add(p); 
            RESULT = whereNodes; :}
                            | search_condition:wns AND predicate:p
            {: ArrayList<WhereNode> whereNodes = wns;
            wns.add(p); 
            RESULT = whereNodes; :};
            
/* Return WhereNode */
predicate 	        ::= pred:p
            {: WhereNode whereNode = p;
            RESULT = p; :}
                        | NOT pred:p
            {: WhereNode whereNode = p;
            p.setNegated();
            RESULT = p; :};
            
/* Return WhereNode */
pred 	                ::= comparison_pred:p
            {: WhereNode whereNode = p;
            RESULT = whereNode; :}
                            | in_pred:p
            {: WhereNode whereNode = p; 
            whereNode.setNestingType("in"); 
            RESULT = whereNode; :}
                            | exists_pred:p
            {: WhereNode whereNode = p;
            whereNode.setNestingType("exists");
            RESULT = whereNode; :};
            
/* Returns WhereNode */   
comparison_pred 	    ::= scalar_exp:sl COMPARISON scalar_exp:sr
            {: WhereNode whereNode = new WhereNode();
            whereNode.setLeftOperandType(sl.getType());
            switch (sl.getType()) {
                case "str":
                    whereNode.setLeftOperandValue(sl.getStrValue());
                    break;
                case "int":
                    whereNode.setLeftOperandValue(sl.getIntValue().toString());
                    break;
                case "col":
                    whereNode.setLeftOperandName(sl.getColValue().getName());
                    break;
            }
            whereNode.setRightOperandType(sr.getType());
            switch (sr.getType()) {
                case "str":
                    whereNode.setRightOperandValue(sr.getStrValue());
                    break;
                case "int":
                    whereNode.setRightOperandValue(sr.getIntValue().toString());
                    break;
                case "col":
                    whereNode.setRightOperandValue(sr.getColValue().getName());
                    break;
            }
            RESULT = whereNode; :};
            
/* Returns Scalar */
scalar_exp 	        ::=     literal:l
            {: Scalar scalar = l;
            RESULT = scalar; :}
                            | column_ref:cr
            {: Scalar scalar = new Scalar("col", cr);
            RESULT = scalar; :};

/* Returns Scalar */
literal 	            ::= STRING:s
            {: RESULT = new Scalar("str", s); :}
                            | INTNUM:i
            {:  RESULT = new Scalar("num", i); :};

/* Returns WhereNode */   
in_pred 	            ::= scalar_exp IN LPAREN select_stmt:s RPAREN
            {: WhereNode whereNode = new WhereNode();
            whereNode.setNestingType("in"); 
            whereNode.setSubQuery(s); 
            RESULT = whereNode; :};
            
/* Returns WhereNode */   
exists_pred 	        ::= EXISTS LPAREN select_stmt:s RPAREN
            {: WhereNode whereNode = new WhereNode();
            whereNode.setNestingType("exists");
            whereNode.setSubQuery(s);
            RESULT = whereNode; :};